pragma ComponentBehavior: Bound
import qs
import qs.services
import qs.modules.common
import qs.modules.common.functions
import QtQuick
import Quickshell
import Quickshell.Io
import Quickshell.Wayland
import Quickshell.Hyprland

Scope {
    id: root

    required property Component lockSurface
    property alias context: lockContext
    property Component sessionLockSurface: WlSessionLockSurface {
        id: sessionLockSurface
        color: "transparent"
        Loader {
            active: GlobalStates.screenLocked
            anchors.fill: parent
            opacity: active ? 1 : 0
            Behavior on opacity {
                animation: Appearance.animation.elementMoveFast.colorAnimation.createObject(this)
            }
            sourceComponent: root.lockSurface
        }
    }

    Process {
        id: unlockKeyringProc
        onExited: (exitCode, exitStatus) => {
            KeyringStorage.fetchKeyringData();
        }
    }
    function unlockKeyring() {
        unlockKeyringProc.exec({
            environment: ({
                "UNLOCK_PASSWORD": lockContext.currentText
            }),
            command: ["bash", "-c", Quickshell.shellPath("scripts/keyring/unlock.sh")]
        })
    }

    // This stores all the information shared between the lock surfaces on each screen.
    // https://github.com/quickshell-mirror/quickshell-examples/tree/master/lockscreen
    LockContext {
        id: lockContext

        Connections {
            target: GlobalStates
            function onScreenLockedChanged() {
                if (GlobalStates.screenLocked) {
                    lockContext.reset();
                    lockContext.tryFingerUnlock();
                }
            }
        }

        onUnlocked: (targetAction) => {
            // Perform the target action if it's not just unlocking
            if (targetAction == LockContext.ActionEnum.Poweroff) {
                Session.poweroff();
                return;
            } else if (targetAction == LockContext.ActionEnum.Reboot) {
                Session.reboot();
                return;
            }

            // Unlock the keyring if configured to do so
            if (Config.options.lock.security.unlockKeyring) root.unlockKeyring(); // Async

            // Unlock the screen before exiting, or the compositor will display a
            // fallback lock you can't interact with.
            GlobalStates.screenLockPending = false;
            GlobalStates.lockAod = false;
            unlockPendingTimer.restart();

            // Reset
            lockContext.reset();

            // Post-unlock actions
            if (lockContext.alsoInhibitIdle) {
                lockContext.alsoInhibitIdle = false;
                Idle.toggleInhibit(true);
            }
        }
    }

    WlSessionLock {
        id: lock
        locked: GlobalStates.screenLocked
        surface: root.sessionLockSurface
        onSecureChanged: {
            if (lock.secure) GlobalStates.startupLockPending = false;
        }
    }

    // AOD lives in the Background window; on niri the lock surface paints its
    // own wallpaper over that, so the sweep would never be seen there.
    readonly property bool aodAvailable: Config.options.lock.aod.enable && WM.compositor !== "niri"

    // Wait the sweep out only when it actually armed — AOD is the idle path
    // alone, so a manual lock still locks immediately. Interval must match the
    // AOD sweep duration in Background.qml.
    Timer {
        id: lockPendingTimer
        interval: GlobalStates.lockAod ? 800 : 100
        onTriggered: GlobalStates.screenLocked = true
    }
    Timer {
        id: unlockPendingTimer
        interval: 100
        onTriggered: GlobalStates.screenLocked = false
    }

    // fromIdle == true only when hypridle's timeout fired it. AOD belongs to
    // that path alone; manual lock, startup lock and before-sleep lock skip it.
    function lock(fromIdle = false) {
        if (Config.options.lock.useHyprlock) {
            Quickshell.execDetached(["bash", "-c", "pidof hyprlock || hyprlock"]);
            return;
        }

        const sweep = fromIdle && root.aodAvailable;

        if (GlobalStates.screenLocked) {
            // Hypridle timed out again while already locked: blacken the screen
            // once more, but never touch the lock itself.
            if (sweep) GlobalStates.lockAod = true;
            return;
        }
        if (GlobalStates.screenLockPending) return;

        GlobalStates.screenLockPending = true;
        // Sweep first; lockPendingTimer locks only after it has covered the
        // screen, so the lockscreen arrives beneath already-black pixels.
        if (sweep) GlobalStates.lockAod = true;
        lockPendingTimer.restart();
    }

    IpcHandler {
        target: "lock"

        function activate(): void {
            root.lock();
        }
        function focus(): void {
            lockContext.shouldReFocus();
        }
    }

    CompositorGlobalShortcut {
        name: "lock"
        description: "Locks the screen"

        onPressed: {
            root.lock()
        }
    }

    CompositorGlobalShortcut {
        name: "lockIdle"
        description: "Locks after an idle timeout, running the AOD sweep first"

        onPressed: {
            root.lock(true)
        }
    }

    CompositorGlobalShortcut {
        name: "lockFocus"
        description: "Re-focuses the lock screen. This is because Hyprland after waking up for whatever reason"
            + "decides to keyboard-unfocus the lock screen"

        onPressed: {
            lockContext.shouldReFocus();
        }
    }

    function initIfReady() {
        if (!Config.ready || !Persistent.ready) return;
        if (Config.options.lock.launchOnStartup && Persistent.isNewHyprlandInstance) {
            root.lock();
        } else {
            KeyringStorage.fetchKeyringData();
            GlobalStates.startupLockPending = false;
        }
    }
    Connections {
        target: Config
        function onReadyChanged() {
            root.initIfReady();
        }
    }
    Connections {
        target: Persistent
        function onReadyChanged() {
            root.initIfReady();
        }
    }
}